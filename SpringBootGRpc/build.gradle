plugins {
	id "com.google.protobuf" version "0.9.4"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

var protobufVersion='3.25.3'
var grpcVersion='1.68.1'

dependencies {
    implementation 'javax.annotation:javax.annotation-api:1.3.2' // https://github.com/grpc/grpc-java/issues/9179
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation platform('com.linecorp.armeria:armeria-bom:1.31.1')
    implementation 'com.linecorp.armeria:armeria'
    implementation 'com.linecorp.armeria:armeria-spring-boot3-starter'
    implementation 'com.linecorp.armeria:armeria-grpc'
	implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/java'
			srcDirs 'build/generated/source/proto/main/grpc'
		}
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

configurations.all {
	resolutionStrategy.eachDependency { details ->
		if ("io.grpc".equalsIgnoreCase(details.requested.group)) {
			details.useVersion grpcVersion
		}
	}
}
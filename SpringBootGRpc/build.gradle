plugins {
	id "com.google.protobuf" version "0.8.17"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

var protobufVersion='3.17.3'
var grpcVersion='1.40.1'

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'io.github.lognet:grpc-spring-boot-starter:4.5.5'
	implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/java'
			srcDirs 'build/generated/source/proto/main/grpc'
		}
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

configurations.all {
	resolutionStrategy.eachDependency { details ->
		if ("io.grpc".equalsIgnoreCase(details.requested.group)) {
			details.useVersion grpcVersion
		}
	}
}